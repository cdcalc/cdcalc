plugins {
    id "com.jfrog.bintray" version "1.8.0"
}

dependencies {
    compile 'org.eclipse.jgit:org.eclipse.jgit:4.11.0.201803080745-r'
    testRuntime 'ch.qos.logback:logback-classic:1.2.3'
}

apply plugin: 'maven-publish'

task sourcesJar(type: Jar, dependsOn: project.classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: project.javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "orjan.sjoholm"
            name "Örjan Sjöholm"
            email "orjansjoholm@gmail.com"
        }
    }
}

publishing {
    publications {
        corePublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.github.cdcalc'
            artifactId 'cdcalc-core'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Calculate upcoming version from git commits')
                root.appendNode('name', 'cdcalc')
                root.appendNode('url', 'https://github.com/cdcalc/cdcalc')
                root.children().last() + pomConfig
            }
        }
    }
}


bintray {
    user = findProperty("bintrayUser")
    key = findProperty("bintrayKey")
    publications = ['corePublication']
    pkg {
        repo = 'cdcalc'
        userOrg = 'cdcalc'
        name = 'cdcalc'
        websiteUrl = "https://github.com/cdcalc/cdcalc"
        vcsUrl = "https://github.com/cdcalc/cdcalc"
        licenses = ["MIT"]
        publish = true
    }
}
