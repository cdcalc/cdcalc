buildscript {
    ext.kotlin_version = '1.3.11'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.github.cdcalc" version "0.0.21"
}

allprojects {
    group = 'com.github.cdcalc'
}

subprojects {
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'kotlin'
    apply plugin: 'jacoco'

    repositories {
        jcenter()
        mavenLocal()
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

        testCompile("org.junit.jupiter:junit-jupiter-api:5.4.0-M1")
        testCompile("org.junit.jupiter:junit-jupiter-params:5.4.0-M1")
        testRuntime("org.junit.jupiter:junit-jupiter-engine:5.4.0-M1")
    }

    test {
        useJUnitPlatform()
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    check.dependsOn jacocoTestReport
}

TaskProvider<Task> calculateVersionTask = tasks.named('calculateVersion')
calculateVersionTask.configure {
    it.doLast() { versionTask ->
        project.findProject(":core").tasks.named("bintrayUpload").configure { Task t ->
            t.setProperty("versionName", versionTask.ext.version)
            t.setProperty("versionVcsTag", "v${versionTask.ext.version}")
        }
    }
}

TaskProvider<Task> releaseSetup = tasks.register('releaseSetup')
releaseSetup.configure {
    it.dependsOn(calculateVersionTask)
}

TaskProvider<Task> releaseBuild = tasks.register('releaseBuild')
releaseBuild.configure {
    it.dependsOn(releaseSetup)
    it.mustRunAfter(releaseSetup)
    it.dependsOn(':core:build', ':plugin:build', ':cli:build')
}

TaskProvider<Task> releasePublish = tasks.register('releasePublish')
releasePublish.configure {
    it.dependsOn(releaseBuild)
    it.dependsOn(':core:bintrayUpload', ':plugin:publishPlugins')
    it.mustRunAfter(releaseBuild)
}

TaskProvider<Task> release = tasks.register('release')
release.configure {
    it.dependsOn(releasePublish)
}

project(':plugin') {
    dependencies {
        compile project(':core')
    }
}

project(':cli') {
    dependencies {
        compile project(':core')
    }
}

wrapper {
    gradleVersion = "5.1"
}
