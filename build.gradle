if (!project.hasProperty("nexusUrl")) {
    ext.nexusUrl = ""
}
if (!project.hasProperty("nexusUsername")) {
    ext.nexusUsername = ""
}
if (!project.hasProperty("nexusPassword")) {
    ext.nexusPassword = ""
}

buildscript {
    ext.kotlin_version = '1.2.20'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    group = 'com.github.cdcalc'
    version = '0.0.9-SNAPSHOT'
}

subprojects {
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'kotlin'
    apply plugin: 'jacoco'

    kotlin {
        experimental {
            coroutines 'enable'
        }
    }

    version = '0.0.9-SNAPSHOT'

    repositories {
        jcenter()
        mavenLocal()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
        testCompile 'junit:junit:4.12'
        testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    check.dependsOn jacocoTestReport
}

project(':plugin') {
    dependencies {
        compile project(':core')
    }
}

project(':cli') {
    dependencies {
        compile project(':core')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "4.4.1"
}
